
package body MyStringTokeniser with SPARK_Mode is



   procedure Tokenise(S : in String; Tokens : in out TokenArray; Count : out Natural) is
      Index : Positive;
      Extent : TokenExtent;
      OutIndex : Integer := Tokens'First;
   begin
      Count := 0;
      if (S'First > S'Last) then
         return;
      end if;
      Index := S'First;
      while OutIndex <= Tokens'Last and Index <= S'Last and Count < Tokens'Length loop

         -- The loop invariant loops through the Token from the first element to the element
         -- specified by OutIndex. Each Token in the loop must have a starting index greater
         -- than or equal to the starting index of the string to ensure the Token is not accessing
         -- memory outside the Tokens array. Each Token should also have length greater than 0
         -- to ensure that memory outside the Tokens array is not accessed or is result of overflow.
         -- Last token index should be also equal or less than the index of the last char in String S
         -- so that the token is not accessing memory outside of the Tokens Array.
         -- This Invariant ensures that the post condition does not fail.
         pragma Loop_Invariant
           (for all J in Tokens'First..OutIndex-1 =>
              (Tokens(J).Start >= S'First and
                   Tokens(J).Length > 0) and then
            Tokens(J).Length-1 <= S'Last - Tokens(J).Start);

         -- Ensures that the OutIndex is always within the bounds of Tokens array and does not
         -- access memory outside of the Tokens array i.e. OutIndex is within Tokens'First and Tokens'Last.
         -- Without this loop invariant then the OutIndex
         -- may access memory outside of the Tokens array or cause overflow when OutIndex is equal to
         -- Integer'First and causes integer overflow
         pragma Loop_Invariant (OutIndex = Tokens'First + Count);

         -- look for start of next token
         while (Index >= S'First and Index < S'Last) and then Is_Whitespace(S(Index)) loop
            Index := Index + 1;
         end loop;
         if (Index >= S'First and Index <= S'Last) and then not Is_Whitespace(S(Index)) then
            -- found a token
            Extent.Start := Index;
            Extent.Length := 0;

            -- look for end of this token
            while Positive'Last - Extent.Length >= Index and then (Index+Extent.Length >= S'First and Index+Extent.Length <= S'Last) and then not Is_Whitespace(S(Index+Extent.Length)) loop
               Extent.Length := Extent.Length + 1;
            end loop;

            Tokens(OutIndex) := Extent;
            Count := Count + 1;

            -- check for last possible token, avoids overflow when incrementing OutIndex
            if (OutIndex = Tokens'Last) then
               return;
            else
               OutIndex := OutIndex + 1;
            end if;

            -- check for end of string, avoids overflow when incrementing Index
            if S'Last - Extent.Length < Index then
               return;
            else
               Index := Index + Extent.Length;
            end if;
         end if;
      end loop;
   end Tokenise;

end MyStringTokeniser;
